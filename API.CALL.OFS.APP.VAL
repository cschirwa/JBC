*-----------------------------------------------------------------------------
* <Rating>267</Rating>
*-----------------------------------------------------------------------------
SUBROUTINE API.CALL.OFS.APP.VAL(LP$OFS.SOURCE.ID, LP$APP.VERSION, LP$FUNC.OPTION,
                            LP$USER, LP$PASSWD, LP$ID, LP$FLDVAL, LP$RESULT)

********************************************************************************
* DESCRIPTION:
*
* API routine
* Builds OFS message based on information supplied, and then feeds it to
* OFS.GLOBUS.MANAGER (the Globus processor).  The result is returned to the
* calling procedure

* VT20100326 - THIS IS AN EXTENTION OF API.CALL.OFS.APP
* Now the message may be processed using OGM or OPM or both, depending on the OPTION parameter (second value of LP$FUNC.OPTION)
*_______________________________________________________________________________
* INCOMING PARAMETERS:
*
* LP$OFS.SOURCE.ID - Key to OFS.SOURCE "GLOBUS"-type record
* LP$APP.VERSION   - Application or Application,Version to run
* LP$FUNC.OPTION   - <1> - Function to use AND <2> the additional OPTION (see Description above)
*                    The OPTION can be "VALIDATE" or "OGM"
* LP$USER          - User under whose credentials the call will run
* LP$PASSWD        - Password of aforementioned user
* LP$ID            - ID of record to work with; empty means generate a new one
* LP$FLDVAL        - Field name (optional :MVNO:SVNO) ..=.. Field value
*                    The delimiter "//" between LD and SCHEDULE legs of OFS should be
*                    in separate field (e.g. ...FM://:FM...)
*_______________________________________________________________________________
* RETURNED DATA:
*
* LP$ID            - ID generated by OFS call (if empty initially)
* LP$RESULT    <1> - Return status (1=OK, -1/-2/-3=error occurred, as per OFS
*                    success/fail indicator, see manuals)
*              <2> - Output error message from OFS (if error occurred,
*                    otherwise OK)
*              <3> - OFS formatted initial message
*              <4> - OFS formatted resulting message
*_______________________________________________________________________________
* NOTES:
*_______________________________________________________________________________
* CHANGELOG:
* ID YYYYMMDD FLASTNAME DESCRIPTION
*
* 01 20040809 LFratila  Initial development
* 02 20050526 VTonu     EN: Modification allowing to add LD.SCHEDULE.DEFINE leg
*                       of OFS in the message
* 03 20090408 VTonu     EN: Modifications required in R06 to call
*                       OFS.POST.MESSAGE
* 04 20100326 VTonu     EN: modifications allowing the OFS message to be processed
*                       using OGM or OPM or both using OGM VALIDATE first,
*                       depending on the OPTION parameter (second value of LP$FUNC.OPTION)
********************************************************************************

$INSERT I_COMMON
$INSERT I_EQUATE

********************************************************************************
* MAIN PROGRAM FLOW
********************************************************************************

    GOSUB INITIALIZE

    GOSUB OFS.HEADER
    GOSUB OFS.BODY
    MSG$VALIDATED = @TRUE ;* by deafault

    IF DO$CALL.OGM THEN

        GOSUB CALL.OGM  ;* will call OFS.GLOBUS.MANAGER with OR without VALIDATION
        GOSUB PARSE.OGM.RESULT
        MSG$VALIDATED = (LP$RESULT<1> = 1)

    END


    IF DO$POST.OFS AND MSG$VALIDATED THEN

       GOSUB POST.OFS   ;* OFS.POST.MESSAGE

    END


RETURN
********************************************************************************
OFS.HEADER:
    * Build header
   L$OFS.HEADER = ""
   L$OFS.HEADER<1> = L$APP                                             ;* ofsOperation
   *L$OFS.HEADER<2> = L$VER:"/":LP$FUNCTION                             ;* ofsOptions
   L$OFS.OPTIONS = L$VER:"/":LP$FUNCTION      ;* this will be added to header in FORMAT.OFS.MSG gosub
   L$OFS.HEADER<3> = (IF LP$USER THEN LP$USER:"/":LP$PASSWD ELSE "")   ;* ofsUserInfo
   L$OFS.HEADER<4> = LP$ID                                             ;* ofsTransactionID

RETURN
********************************************************************************
OFS.BODY:
    * Build body
   L$OFS.BODY = ""
   FOR I = 1 TO DCOUNT(LP$FLDVAL, FM)

      * [CHG.02 <]
      IF LP$FLDVAL<I> =SCHEDULE.DELIMITER THEN
          L$OFS.BODY<-1> = LP$FLDVAL<I>
      END ELSE
      * [CHG.02 >]

          L$FLD = FIELD(LP$FLDVAL<I>, "=", 1, 1)
          L$VAL = FIELD(LP$FLDVAL<I>, "=", 2, 999)
          * Ensure syntax is correct if no MV/SV numbers supplied
          IF NOT(INDEX(L$FLD, ":", 1)) THEN L$FLD := "::"
          * Replace commas with question marks (as per OFS message syntax notes)
          CONVERT "," TO "?" IN L$VAL
          L$OFS.BODY<-1> = L$FLD :"=" :L$VAL
      END
   NEXT I
RETURN
********************************************************************************
INITIALIZE:

   SCHEDULE.DELIMITER='//' ;* [CHG.02]

   L$OFS.MSG = ""
   L$APP = FIELD(LP$APP.VERSION, ",", 1)
   L$VER = FIELD(LP$APP.VERSION, ",", 2)

   CONVERT VM:"/" TO FM:FM IN LP$FUNC.OPTION
   LP$FUNCTION = LP$FUNC.OPTION<1>
   LP$OPTION   = LP$FUNC.OPTION<2>

   GOSUB CHECK.OPTION

   LP$RESULT = ''
   RETURN
********************************************************************************
CHECK.OPTION:

    DO$VALIDATE = (LP$OPTION[1,3] = "VAL")

    DO$JUST.OGM = ((LP$OPTION = "OLD") OR (LP$OPTION = "OGM"))

    DO$POST.OFS = DO$VALIDATE OR NOT(DO$JUST.OGM)

    DO$CALL.OGM = DO$JUST.OGM OR DO$VALIDATE
   RETURN
********************************************************************************
FORMAT.OFS.MSG:
   L$OFS.OPTIONS["/", 3, 1] = L$OFS.OPERATION.FLAG
   L$OFS.HEADER<2> = L$OFS.OPTIONS
   L$OFS.MSG = L$OFS.HEADER : FM : L$OFS.BODY

    * [CHG.02 <]
   L$OFS.MSG=CHANGE(L$OFS.MSG, FM:SCHEDULE.DELIMITER:FM,SCHEDULE.DELIMITER)
   * [CHG.02 >]
   CONVERT FM TO "," IN L$OFS.MSG

   LP$RESULT<3> = L$OFS.MSG

RETURN
********************************************************************************
CALL.OGM:

    IF DO$VALIDATE THEN
        L$OFS.OPERATION.FLAG = "VALIDATE"
    END

    GOSUB FORMAT.OFS.MSG

    CALL OFS.GLOBUS.MANAGER(LP$OFS.SOURCE.ID, L$OFS.MSG)

   RETURN
********************************************************************************
POST.OFS:

    L$OFS.OPERATION.FLAG = ""

    GOSUB FORMAT.OFS.MSG

    CALL OFS.POST.MESSAGE(L$OFS.MSG, L$OFS.MSG.ID, LP$OFS.SOURCE.ID, '')

    LP$RESULT<1> = 1
    LP$RESULT<2> = "OK"
    LP$RESULT<4> = ",":L$OFS.MSG.ID; * in a lot of subroutines the ofs result

   RETURN
********************************************************************************
PARSE.OGM.RESULT:
    SENSITIVITY=''
    * Retrieve OFS exit status (success/fail indicator)
    LP$RESULT<1> = FIELD(FIELD(L$OFS.MSG, "/", 3, 1), ",", 1, 1)
    * Retrieve new transaction ID, if necessary
    IF NOT(LP$ID) THEN LP$ID = FIELD(L$OFS.MSG, "/", 1, 1)

    BEGIN CASE
    CASE LP$RESULT<1> < 0
       * Retrieve error details
       LP$RESULT<2> = FIELD(L$OFS.MSG, ",", 2, 1)
    CASE LP$RESULT<1> = ""
       * In case that OFS didn't even get the chance to run, and instead
       * returned a more general error message
       LP$RESULT<1> = -1 ;* set it anyway
       LP$RESULT<2> = L$OFS.MSG
    CASE LP$RESULT<1> = 1
       LP$RESULT<2> = "OK"
    END CASE

    LP$RESULT<4> = L$OFS.MSG
   RETURN

END
